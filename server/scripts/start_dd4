#! /bin/csh -f
# Written by Furey.
# With additions from Tony.
# With changes from Kahn.

# Set the port number.
set port = 8888
if ( "$1" != "" ) set port="$1"

# Gezhp 2002
rm nohup.out

# Change to area directory.
cd ../area

# Set limits.
nohup
limit stack 2048k
if ( -e SHUTDOWN.TXT ) rm -f SHUTDOWN.TXT

while ( 1 )
    # If you want to have logs in a different directory,
    #   change the 'set logfile' line to reflect the directory name.
    set index = 1000
    while ( 1 )
	set logfile = ../log/$index.log
	if ( ! -e $logfile ) break
	@ index++
    end

    # Update to new code if possible.
    if ( -e ../src/envy.new ) then
	\mv ../src/envy ../src/envy.old
	\mv ../src/envy.new ../src/envy
    endif

    # Compile last command list
    if ( -e ../log/last_command.txt ) then
        \date >> ../log/last_command_list.txt
    	\cat ../log/last_command.txt >> ../log/last_command_list.txt
	\rm ../log/last_command.txt
    endif	

    # Run envy.
    ../src/envy $port >&! $logfile

    if ( -e core ) then
	\mv core ../src/
    endif

    # Restart, giving old connections a chance to die.
    if ( -e SHUTDOWN.TXT ) then
	rm -f SHUTDOWN.TXT
	exit 0
    endif

    sleep 5
end
