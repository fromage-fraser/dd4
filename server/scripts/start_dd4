#! /bin/csh -f
# Written by Furey.
# With additions from Tony.
# With changes from Kahn.
# With changes from Gezhp.
# With changes from Owl.

# Set the port number.
set port = 8888
if ( "$1" != "" ) set port="$1"

# Gezhp 2002 / Owl 2022
if ( -e nohup.out ) rm nohup.out

# Change to area directory.
cd ../area

# Set limits.
nohup
limit stack 2048k
if ( -e SHUTDOWN.TXT ) rm -f SHUTDOWN.TXT

while ( 1 )
    # If you want to have logs in a different directory,
    #   change the 'set logfile' line to reflect the directory name.
    set index = 1000
    while ( 1 )
	set logfile = ../log/$index.log
	if ( ! -e $logfile ) break
	@ index++
    end

    # Update to new code if possible.
    if ( -e ../src/dd4.new ) then
	\mv ../src/dd4 ../src/dd4.old
	\mv ../src/dd4.new ../src/dd4
    endif

    # Compile last command list
    if ( -e ../log/last_command.txt ) then
        \date >> ../log/last_command_list.txt
    	\cat ../log/last_command.txt >> ../log/last_command_list.txt
	\rm ../log/last_command.txt
    endif

    # Run envy.
    ../src/dd4 $port >&! $logfile

    if ( -e core ) then
	\mv core ../src/
    endif

    # Restart, giving old connections a chance to die.
    if ( -e SHUTDOWN.TXT ) then
	rm -f SHUTDOWN.TXT
	exit 0
    endif

    sleep 5
end
